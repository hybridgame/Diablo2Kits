BACKUP ~d2#pal/backup~
AUTHOR ~HybridGame~
VERSION "1.0.20190704"

LANGUAGE    ~Korean~
            ~Korean~
            ~d2#pal/lang/korean/diablo2.tra~
            ~d2#pal/lang/korean/setup.tra~

///////////////////////////////////////////////////////////////////////////////////////////////////
// D2 Paladin

BEGIN ~D2 Paladin kit for Paladin~
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee~ @1

INCLUDE "d2#pal/lib/fl#add_kit_ee.tpa"
INCLUDE "d2#pal/lib/weidu.tpa"
INCLUDE "d2#pal/lib/d2#action.tpa"
INCLUDE "d2#pal/lib/d2#patch.tpa"

// Add kit.
PRINT ~===== Adding kit.~
ADD_KIT ~d2#pal~
    // CLASWEAP.2DA : BG1 proficiencies. (deprecated)
    ~d2#pal   1 1 1 1 1 1 1 1~
    
    // WEAPPROF.2DA : BG2 proficiencies
    //11                                T   S           F   Q             S S
    //10                                W   C           L   U             W I
    //9                         B L S   O   I   W       A   A             O N
    //8                         A O H   H   M   A       I   R C   S       R G
    //7         L S           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
    //6         _ _       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
    //5         S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
    //4         W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
    //3         O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
    //2        	R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
    //1      	D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
    ~d2#pal     2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 0 0 0 0 0 2 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  
    // ABCLASRQ.2DA : Minimum stats at character creation
    ~d2#pal   16 14 16 10 13 17~
    
    // ABCLSMOD.2DA : Stat modifiers at character creation
    ~d2#pal   0 0 0 0 0 0~
    
    // ABDCDSRQ.2DA : Stats requirement to dual-class
    ~d2#pal   0 17 0 0 0 0~
    
    // ABDCSCRQ.2DA : Stats requirement to dual-class
    ~d2#pal   0 0 0 17 0 0~
    
    //        LG LN LE NG TN NE CG CN CE ALIGNMNT.2DA : Selectable alignment
    ~d2#pal   1  0  0  0  0  0  0  0  0~
    
    //        F C M T D R DUALCLAS.2DA : Dual possible classes
    ~d2#pal   0 0 0 0 0 0~
    
    // Kit's own clab*.2da
    ~d2#pal/2da/d2#pal.2da~
    
    // KITTABLE.2DA : Selectable race
    ~K_P_H~
    
    // KITLIST.2DA : Kit's usability flag
    ~0x00000010 6~
    
    // High level abilities. prefix lu file required
    ~d2#pal~
    
    // 25stweap.2da : TOB starting items
    ~* * * * * * * * * * * * * * * * * * * *~
    
    // Lowercase name
    SAY @101
    
    // First uppercase name
    SAY @102
    
    // Description
    SAY @103

// Kit detail.
LAF fl#add_kit_ee
    STR_VAR
        kit_name = ~d2#pal~
        hpclass = ~HPWAR~
END

// High level abilities 2da file
COPY ~d2#pal/2da/lud2#pal.2da~ ~override~

// Sound
PRINT ~===== Copying sound files.~
COPY ~d2#pal/snd~ ~override~

///////////////////////////////////////////////////////////////////////////////////////////////////

OUTER_TEXT_SPRINT d2Kit ~d2#pal~
OUTER_TEXT_SPRINT d2Pre ~d2#12~
OUTER_TEXT_SPRINT d2Num ~12~

OUTER_TEXT_SPRINT d2Lib ~d2#pal/lib~
OUTER_TEXT_SPRINT d2Bam ~d2#pal/bam~
OUTER_TEXT_SPRINT d2Baf ~d2#pal/baf~
OUTER_TEXT_SPRINT d2Pro ~d2#pal/pro~

ACTION_DEFINE_ARRAY d2TabLevel BEGIN 0 1 4 8 12 15 18 21 24 27 30 33 36 39 END
ACTION_DEFINE_ARRAY d2CombatSkill BEGIN 21 24 26 END
ACTION_DEFINE_ARRAY d2AuraSkill BEGIN 01 02 03 04 05 07 10 11 12 14 15 16 17 19 20 END

OUTER_SET d2CombatSkillCount = 3
OUTER_SET d2AuraSkillCount = 15
OUTER_SET d2TabCount = 4

///////////////////////////////////////////////////////////////////////////////////////////////////

// Skill point
PRINT ~===== Creating skill points.~
LAF CreateSkillSelecting END

///////////////////
// Defensive Auras
PRINT ~===== Creating Defensive Auras skills.~

// Prayer
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_REGENERATION END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 5 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN "-1" END
ACTION_DEFINE_ARRAY d2Param2 BEGIN 3 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1201) desc = RESOLVE_STR_REF(@1231) skillLevel = 1
    STR_VAR skill = "01"
END

// Resist Fire
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_RES_FIRE END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 20 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 10 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1202) desc = RESOLVE_STR_REF(@1232) skillLevel = 1
    STR_VAR skill = "02"
END

// Resist Cold
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_RES_COLD END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 20 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 10 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1203) desc = RESOLVE_STR_REF(@1233) skillLevel = 2
    STR_VAR skill = "03"
END

// Resist Lightning
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_RES_ELEC END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 20 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 10 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1204) desc = RESOLVE_STR_REF(@1234) skillLevel = 3
    STR_VAR skill = "04"
END

// Defiance
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_AC END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 1 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 1 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1205) desc = RESOLVE_STR_REF(@1235) skillLevel = 2
    STR_VAR skill = "05"
END

// Vigor
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_MOVEMENT END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 1 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 1 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1207) desc = RESOLVE_STR_REF(@1237) skillLevel = 4
    STR_VAR skill = "07"
END

// Salvation
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_RES_ELEC OP_RES_FIRE OP_RES_COLD OP_RES_ACID END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 20 20 20 20 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 10 10 10 10 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1210) desc = RESOLVE_STR_REF(@1240) skillLevel = 6
    STR_VAR skill = "10"
END

///////////////////
// Offensive Auras
PRINT ~===== Creating Offensive Auras skills.~

ADD_PROJECTILE ~%d2Pro%/d2#np15.pro~
ADD_PROJECTILE ~%d2Pro%/d2#np20.pro~
ADD_PROJECTILE ~%d2Pro%/d2#np25.pro~
ADD_PROJECTILE ~%d2Pro%/d2#np30.pro~
ACTION_DEFINE_ARRAY d2Projectile BEGIN d2#np15 d2#np20 d2#np25 d2#np30 END

// Might
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_DAMAGE END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 2 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 1 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1211) desc = RESOLVE_STR_REF(@1241) skillLevel = 1
    STR_VAR skill = "11"
END

// Holy Fire
LAM ClearArray
ACTION_DEFINE_ARRAY d2Melee BEGIN 3 1 END
ACTION_DEFINE_ARRAY d2Range BEGIN 1 1 END
LAF CreateDamagingAura
    INT_VAR name = RESOLVE_STR_REF(@1212) desc = RESOLVE_STR_REF(@1242)
            skillLevel = 2 damageType = DAMAGE_FIRE
    STR_VAR skill = "12"
END

// Blessed Aim
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_THAC0 END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 4 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 2 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1214) desc = RESOLVE_STR_REF(@1244) skillLevel = 3
    STR_VAR skill = "14"
END

// Concentration
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_DAMAGE END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 4 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 1 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1215) desc = RESOLVE_STR_REF(@1245) skillLevel = 4
    STR_VAR skill = "15"
END

// Holy Freeze
LAM ClearArray
ACTION_DEFINE_ARRAY d2Melee BEGIN 4 1 END
ACTION_DEFINE_ARRAY d2Range BEGIN 1 1 END
LAF CreateDamagingAura
    INT_VAR name = RESOLVE_STR_REF(@1216) desc = RESOLVE_STR_REF(@1246)
            skillLevel = 4 damageType = DAMAGE_COLD
    STR_VAR skill = "16"
END

// Holy Shock
LAM ClearArray
ACTION_DEFINE_ARRAY d2Melee BEGIN 0 0 1 0 6 3 END
ACTION_DEFINE_ARRAY d2Range BEGIN 0 0 1 0 3 2 END
LAF CreateDamagingAura
    INT_VAR name = RESOLVE_STR_REF(@1217) desc = RESOLVE_STR_REF(@1247)
            skillLevel = 5 damageType = DAMAGE_ELEC
    STR_VAR skill = "17"
END

// Fanaticism
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_ATTACK_PER_ROUND OP_THAC0 OP_DAMAGE END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 6 1 2 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 0 1 1 END
LAF CreateBuffAura
    INT_VAR name = RESOLVE_STR_REF(@1219) desc = RESOLVE_STR_REF(@1249)
            skillLevel = 6
    STR_VAR skill = "19"
END

// Conviction
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_AC OP_RES_ELEC OP_RES_FIRE OP_RES_COLD OP_RES_ACID END
ACTION_DEFINE_ARRAY d2Param1 BEGIN "-2" "-12" "-12" "-12" "-12" END
ACTION_DEFINE_ARRAY d2P1Step BEGIN "-1" "-6" "-6" "-6" "-6" END
LAF CreateDebuffAura
    INT_VAR name = RESOLVE_STR_REF(@1220) desc = RESOLVE_STR_REF(@1250) skillLevel = 6
    STR_VAR skill = "20"
END

///////////////////
// Combat Skills
PRINT ~===== Creating Combat Skills skills.~

// Holy Bolt
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_HP_DAMAGE OP_CURRENT_HP END
ACTION_DEFINE_ARRAY d2Param2 BEGIN DAMAGE_MAGIC 0 END
ACTION_DEFINE_ARRAY d2Dice BEGIN 4 4 END
ACTION_DEFINE_ARRAY d2DStep BEGIN 2 2 END
ACTION_DEFINE_ARRAY d2DiceSize BEGIN 10 5 END
ACTION_DEFINE_ARRAY d2IDSFile BEGIN IDS_FILE_GENERAL IDS_FILE_EA END
ACTION_DEFINE_ARRAY d2IDSEntry BEGIN GENERAL_UNDEAD EA_PC END
LAF CreateTargetSkill
    INT_VAR name = RESOLVE_STR_REF(@1223) desc = RESOLVE_STR_REF(@1253)
            skillLevel = 2 timing = TIMING_PERMANENT projectile = 51    // 51-Sparkle_Stone (SPARKLST)
    STR_VAR skill = "23"
END

// Zeal
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_ATTACK_PER_ROUND OP_THAC0_MELEE OP_DAMAGE_MELEE END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 6 1 1 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 0 1 1 END
LAF CreateCombatSkill
    INT_VAR name = RESOLVE_STR_REF(@1224) desc = RESOLVE_STR_REF(@1254) skillLevel = 3
    STR_VAR skill = "24"
END

// Vengeance
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_HP_DAMAGE OP_HP_DAMAGE OP_HP_DAMAGE OP_HP_DAMAGE OP_THAC0_MELEE END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 0 0 0 0 2 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 0 0 0 0 1 END
ACTION_DEFINE_ARRAY d2Param2 BEGIN DAMAGE_FIRE DAMAGE_COLD DAMAGE_ELEC DAMAGE_ACID END
ACTION_DEFINE_ARRAY d2Dice BEGIN 1 1 1 1 0 END
ACTION_DEFINE_ARRAY d2DiceSize BEGIN 2 2 2 2 0 END
ACTION_DEFINE_ARRAY d2DSStep BEGIN 1 1 1 1 0 END
LAF CreateCombatSkill
    INT_VAR name = RESOLVE_STR_REF(@1226) desc = RESOLVE_STR_REF(@1256)
            skillLevel = 4 effectCount = 4
    STR_VAR skill = "26"
END

// Blessed Hammer
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_HP_DAMAGE OP_HP_DAMAGE END
ACTION_DEFINE_ARRAY d2Param2 BEGIN DAMAGE_MAGIC DAMAGE_MAGIC END
ACTION_DEFINE_ARRAY d2Dice BEGIN 4 4 END
ACTION_DEFINE_ARRAY d2DStep BEGIN 2 2 END
ACTION_DEFINE_ARRAY d2DiceSize BEGIN 10 10 END
ACTION_DEFINE_ARRAY d2IDSFile BEGIN IDS_FILE_GENERAL END
ACTION_DEFINE_ARRAY d2IDSEntry BEGIN GENERAL_UNDEAD END
LAF CreateTargetSkill
    INT_VAR name = RESOLVE_STR_REF(@1227) desc = RESOLVE_STR_REF(@1257)
            skillLevel = 4 timing = TIMING_PERMANENT projectile = 11    // 11-Axe_Shocking (AXE)
    STR_VAR skill = "27"
END

// Holy Shield
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_RES_POISON OP_RES_ELEC OP_RES_FIRE OP_RES_COLD OP_RES_ACID
    OP_RES_SLASH OP_RES_CRUSH OP_RES_PIERCE OP_RES_MISSILE OP_RES_MAGIC_DAMAGE OP_RES_MAGIC END
ACTION_DEFINE_ARRAY d2Param1 BEGIN 100 12 12 12 12 12 12 12 12 12 12 END
ACTION_DEFINE_ARRAY d2P1Step BEGIN 0 6 6 6 6 6 6 6 6 6 6 END
LAF CreateTargetSkill
    INT_VAR name = RESOLVE_STR_REF(@1229) desc = RESOLVE_STR_REF(@1259)
            skillLevel = 5 tabTarget = TAB_TARGET_SELF duration = 120 durStep = 60
    STR_VAR skill = "29"
END

// Fist of the Heavens
LAM ClearArray
ACTION_DEFINE_ARRAY d2Opcode BEGIN OP_HP_DAMAGE END
ACTION_DEFINE_ARRAY d2Param2 BEGIN DAMAGE_ELEC END
ACTION_DEFINE_ARRAY d2Dice BEGIN 8 END
ACTION_DEFINE_ARRAY d2DStep BEGIN 4 END
ACTION_DEFINE_ARRAY d2DiceSize BEGIN 10 END
LAF CreateTargetSkill
    INT_VAR name = RESOLVE_STR_REF(@1230) desc = RESOLVE_STR_REF(@1260)
            skillLevel = 6 timing = TIMING_PERMANENT projectile = 255   // 255-Holylight_Pillar ()
    STR_VAR skill = "30"
END